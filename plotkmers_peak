reset 
breakpoint=breakpoint+0
ymin1=0.0
#keyxpos=start+25
#keyypos=ymax2 - ymax2 / 10

set output temp1
set terminal svg size 1300,200
set multiplot


bm = 0.225
lm = 0.016
rm = 0.981
gap = 0.03
size = 0.7
y1 = ymin1
y2 = ymax1
y3 = ymin2
y4 = ymax2
scale=(abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) )
scale=1.0/2.0

# bottom plot
set xrange [start:end]
unset xtics
unset ytics
unset border
set bmargin at screen bm
set xtics 50 font ", 11" nomirror
set border 1+2+8
set lmargin at screen lm
set rmargin at screen rm
set tmargin at screen bm + size * scale
set yrange [y1:y2]
set ytics ytic1 font ", 11" mirror
set tics out
plot kmer_count using 1:3 smooth freq  lw 1 lc rgb "#1b9e77" notitle


# draw the four diagonal lines on y axis
set arrow from screen lm - gap / 4.0, bm + size * scale - gap / 4.0 to screen lm + gap / 4.0, bm + size * scale + gap / 4.0 nohead
set arrow from screen lm - gap / 4.0, bm + size * scale - gap / 4.0  + gap to screen lm + gap / 4.0, bm + size * scale + gap / 4.0 + gap nohead
set arrow from screen rm - gap / 4.0, bm + size * scale - gap / 4.0 to screen rm + gap / 4.0, bm + size * scale + gap / 4.0 nohead
set arrow from screen rm - gap / 4.0, bm + size * scale - gap / 4.0  + gap to screen rm + gap / 4.0, bm + size * scale + gap / 4.0 + gap nohead

# top plot
set ytics ytic2 font ", 11" mirror
set tics out
unset xtics
unset xlabel
set border 2+4+8
set bmargin at screen bm + size * scale + gap
set tmargin at screen bm + size + gap
set yrange [y3:y4]
plot kmer_count using 1:3 smooth freq  lw 1 lc rgb "#1b9e77" notitle
